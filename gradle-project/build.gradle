plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
}

allprojects {
    group = 'by.gstu.project'
    version = '1.0.1'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.0'
    }

    test {
        useJUnitPlatform()
        finalizedBy jacocoTestReport
    }

    jacocoTestReport {
        dependsOn test
        reports {
            html.required = true
            xml.required = true
        }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                name = "GitHub"
                url = "https://maven.pkg.github.com/ulmie140/library-system"
                credentials {
                    username = project.findProperty("githubUser") as String
                    password = project.findProperty("githubToken") as String
                }
            }
        }
    }
}

// Собственная задача
tasks.register('hello') {
    doLast {
        println 'Hello, Gradle!'
    }
}